start: (font_definition | color_definition | type_definition | canvas_definition)*

identifier: CNAME

_IWS: WS_INLINE
_WS: WS
_NL: NEWLINE

color_definition: "define" _IWS "color" _IWS identifier _IWS hex_color _IWS? _NL
hex_color: HEX_COLOR
HEX_COLOR: "#" HEXDIGIT HEXDIGIT HEXDIGIT [HEXDIGIT HEXDIGIT HEXDIGIT]

font_definition: "define" _IWS "font" _IWS identifier _IWS font_name _IWS font_size [_IWS font_weight] _IWS? _NL
font_name: ESCAPED_STRING
font_size: INT
FONT_WEIGHT: "thin" | "regular" | "bold"
font_weight: FONT_WEIGHT

type_definition: "define" _IWS identifier [_IWS int_width _IWS int_height] _item_body
int_width: INT
int_height: INT
_item_body: _IWS? "{" _WS? item_list _WS? "}" _NL? _WS?
item_list: (item _NL _WS?)* [item _WS?]

file_name: ESCAPED_STRING

canvas_definition: "canvas" _IWS file_name _IWS size _item_body

item: rect | circle | ellipse | line | polygon | text | image | custom

color: identifier | hex_color

modifier_list: (modifier _IWS)*
modifier: STATIC_MODIFIER | outlined | rotated
STATIC_MODIFIER: "left"
    | "right"
    | "center"
    | "clipped"
outlined: "outlined" _IWS color _IWS thickness [_IWS opacity]
rotated: "rotated" _IWS angle

expression: INT
_expr_tuple: expression _IWS expression
position: _expr_tuple
size: _expr_tuple
thickness: INT
opacity: INT
angle: SIGNED_INT
font: CNAME
content: ESCAPED_STRING
path: ESCAPED_STRING

rect: modifier_list "rect" _IWS position _IWS size [_IWS color] [_item_body]
circle: modifier_list "circle" _IWS position _IWS expression [_IWS color] [_item_body]
ellipse: modifier_list "ellipse" _IWS position _IWS expression _IWS expression [_IWS color] [_item_body]
line: "line" _IWS position _IWS position [_IWS color] [_IWS thickness]
polygon: modifier_list "polygon" _IWS position _IWS position _IWS (position _IWS)* [_IWS color] [_item_body]
text: modifier_list "text" _IWS position _IWS font _IWS color [_IWS expression] _IWS content
image: modifier_list "image" _IWS position _IWS expression [_IWS expression] _IWS path
custom: identifier _IWS position [_IWS size]

%import common.CNAME
%import common.WS_INLINE
%import common.WS
%import common.ESCAPED_STRING
%import common.INT
%import common.SIGNED_INT
%import common.NEWLINE
%import common.HEXDIGIT