start: (font_definition | color_definition | type_definition | canvas_definition | _comment)*

identifier: CNAME

_WHITESPACE: WS
_NEWLINE: NEWLINE

color_definition: "define color" identifier hex_color (_comment? | _NEWLINE)
hex_color: HEX_COLOR
HEX_COLOR: "#" HEXDIGIT HEXDIGIT HEXDIGIT [HEXDIGIT HEXDIGIT HEXDIGIT]

font_definition: "define font" identifier font_name font_size [font_weight] (_comment? | _NEWLINE)
font_name: ESCAPED_STRING
font_size: INT
FONT_WEIGHT: "thin" | "regular" | "bold"
font_weight: FONT_WEIGHT

type_definition: "define" identifier [int_width int_height] _item_body
int_width: INT
int_height: INT
_item_body: "{" _WHITESPACE? item_list _WHITESPACE? "}" _NEWLINE? _WHITESPACE?
item_list: (item _NEWLINE _WHITESPACE?)* [item _WHITESPACE?]

file_name: ESCAPED_STRING

canvas_definition: "canvas" file_name size _item_body

item: rect | circle | ellipse | line | polygon | text | image | custom

color: identifier | hex_color

modifier_list: modifier*
modifier: STATIC_MODIFIER | outlined | rotated
STATIC_MODIFIER: "left"
    | "right"
    | "center"
    | "clipped"
outlined: "outlined" color thickness [opacity]
rotated: "rotated" angle

?sum: product
    | sum "+" product         -> add
    | sum "-" product         -> sub

?product: atom
    | product "*" atom        -> mul
    | product "/" atom        -> div

?atom: SIGNED_INT [unit]   -> value
     | "-" atom               -> neg
     | "(" sum ")"

unit: UNIT -> unit

UNIT: "h" | "w"

expression: atom
_expr_tuple: expression expression
position: _expr_tuple
size: _expr_tuple
thickness: INT
opacity: INT
angle: SIGNED_INT
font: CNAME
content: ESCAPED_STRING
path: ESCAPED_STRING

rect: modifier_list "rect" position size [color] [_item_body]
circle: modifier_list "circle" position expression [color] [_item_body]
ellipse: modifier_list "ellipse" position expression expression [color] [_item_body]
line: "line" position position [color] [thickness]
polygon: modifier_list "polygon" position position+ [color] [_item_body]
text: modifier_list "text" position font color [expression] content
image: modifier_list "image" position expression [expression] path
custom: identifier position [size]

_comment: _NEWLINE? (_INLINE_COMMENT | _MULTILINE_COMMENT) _NEWLINE?

%import common.CNAME
%import common.WS_INLINE
%import common.WS
%import common.ESCAPED_STRING
%import common.INT
%import common.SIGNED_INT
%import common.NEWLINE
%import common.HEXDIGIT
%import common.C_COMMENT -> _MULTILINE_COMMENT
%import common.CPP_COMMENT -> _INLINE_COMMENT

%ignore WS_INLINE